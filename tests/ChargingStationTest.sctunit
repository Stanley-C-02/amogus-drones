testclass ChargingStationTest for statechart ChargingStation {
	
	// Testing availability only when turned on
	@Test
	operation testAvailable() {
		enter
			assert !active (active_charging_station_status.On.charging_station_availability.Available)
			raise turnOn
			assert active (active_charging_station_status.On.charging_station_availability.Available)
			raise turnOff
			assert !active (active_charging_station_status.On.charging_station_availability.Available)
		exit
	}
	
	// Testing Reserve
	@Test
	operation testReservation() {
		enter
			raise turnOn 
			raise reserve
			assert active (active_charging_station_status.On.charging_station_availability.Reserved)
			assert !active (active_charging_station_status.On.charging_station_availability.Available)
		exit			
	}
	
	//Test charging only when reserved
	@Test
	operation testChargingWhenReserved() {
		enter
			raise turnOn
			raise startCharging
			assert !active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Charging)
			raise reserve
			assert !active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Charging)
			raise startCharging
			assert active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Charging)
		exit
	}
	
	//Test releasing only when idle (not connected)
	@Test
	operation testReleaseWhenIdle() {
		enter
			raise turnOn
			assert active (active_charging_station_status.On.charging_station_availability.Available)
			raise reserve
			assert active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Idle)
			raise release
			assert active (active_charging_station_status.On.charging_station_availability.Available)
		exit
	}
	
	//Test releasing when charging (connected)
	@Test
	operation testReleaseWhenCharging() {
		enter
			raise turnOn
			assert active (active_charging_station_status.On.charging_station_availability.Available)
			raise reserve
			assert active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Idle)
			raise startCharging
			assert active (active_charging_station_status.On.charging_station_availability.Reserved.charging_status.Charging)
			raise release
			assert active (active_charging_station_status.On.charging_station_availability.Available)
		exit
	}
}
